buildscript {
    repositories {
        google()
        jcenter()
    }

    apply from: 'dependencies.gradle'

    dependencies {
        classpath buildScript.android
        classpath buildScript.firebase
        classpath buildScript.googleServices
        classpath buildScript.gradleStaticAnalysisPlugin
        classpath buildScript.playPublisher
    }
}

plugins {
    id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC6-4' apply false
    id 'com.github.ben-manes.versions' version '0.17.0'
}

def teamPropsFile(propsFile) {
    def teamPropsDir = file('team-props')
    return new File(teamPropsDir, propsFile)
}

apply from: 'dependencies.gradle'
apply from: teamPropsFile('git-hooks.gradle')

subprojects {
    buildscript {
        repositories {
            google()
            jcenter()
            maven { url 'https://maven.fabric.io/public' }
            maven { url 'http://dl.bintray.com/arturbosch/code-analysis' }
        }
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    if (project.name != 'renderthread') {
        apply from: teamPropsFile('static-analysis.gradle')
    }
}

apply from: teamPropsFile('android-code-quality.gradle')

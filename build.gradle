buildscript {
    repositories {
        google()
        jcenter()
    }

    apply from: 'dependencies.gradle'

    dependencies {
        classpath buildScript.android
        classpath buildScript.gradleStaticAnalysisPlugin
        classpath buildScript.gradleVersionsPlugin
        classpath buildScript.googleServices
        classpath buildScript.jaCoCo
        classpath buildScript.playPublisher
    }
}

plugins {
    id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC6-2'
}

def teamPropsFile(propsFile) {
    def teamPropsDir = file('team-props')
    return new File(teamPropsDir, propsFile)
}

apply from: 'dependencies.gradle'
apply from: teamPropsFile('git-hooks.gradle')

detekt {
    profile('main') {
        filters = '.*'        // Nothing to check in the root project
    }
}

subprojects {
    buildscript {
        repositories {
            google()
            jcenter()
            maven { url 'https://maven.fabric.io/public' }
            maven { url 'http://dl.bintray.com/arturbosch/code-analysis' }
        }
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    apply plugin: 'com.github.ben-manes.versions'
    apply from: teamPropsFile('static-analysis.gradle')
}

apply from: teamPropsFile('android-code-quality.gradle')

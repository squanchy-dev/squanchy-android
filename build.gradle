buildscript {
    repositories {
        google()
        jcenter()
    }

    apply from: 'dependencies.gradle'

    dependencies {
        classpath buildScript.android
        classpath buildScript.firebase
        classpath buildScript.googleServices
        classpath buildScript.gradleStaticAnalysisPlugin
        classpath buildScript.playPublisher
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC8'
    id 'org.jlleitschuh.gradle.ktlint' version '5.0.0'
}

def teamPropsFile(propsFile) {
    def teamPropsDir = file('team-props')
    return new File(teamPropsDir, propsFile)
}

apply from: 'dependencies.gradle'
apply from: teamPropsFile('git-hooks.gradle')

subprojects {
    buildscript {
        repositories {
            google()
            maven { url 'https://maven.fabric.io/public' }
            maven { url 'http://dl.bintray.com/arturbosch/code-analysis' }
            jcenter()
        }
    }

    repositories {
        google()
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'com.github.ben-manes.versions'
}

apply from: teamPropsFile('setup-root-tasks.gradle')
